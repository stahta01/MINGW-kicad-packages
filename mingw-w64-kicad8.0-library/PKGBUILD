# Support libraries for KiCad

# Based partly on https://archlinux.org/packages/extra/any/kicad-library/ and
# https://github.com/msys2/MINGW-packages/blob/master/mingw-w64-kicad-library/PKGBUILD

PATH=/usr/local/bin:/usr/bin:/bin:/opt/bin
MSYSTEM="MSYS"
MINGW_COMMON_PREFIX="/mingw-common"
MINGW_PACKAGE_PREFIX="mingw-common"

_realname=kicad
_basepkgver=8.0
_basepkgname=${_realname}${_basepkgver}
_sharefoldername=${_realname}${_basepkgver}
pkgbase=mingw-w64-${_realname}${_basepkgver}-library
pkgname=("${MINGW_PACKAGE_PREFIX}-${_basepkgname}-library")
pkgver=8.0.3
pkgrel=1
pkgdesc="Symbol, footprint and template libraries for KiCad ${_basepkgver} (mingw-common)"
arch=('any')
mingw_arch=('mingw-common')
url='https://www.kicad.org/libraries/'
license=("Creative Commons CC-BY-SA 4.0 License")
groups=("${MINGW_PACKAGE_PREFIX}-eda")
makedepends=('cmake' 'make')
options=('!strip')
source=("https://gitlab.com/kicad/libraries/kicad-footprints/-/archive/${pkgver}/kicad-footprints-${pkgver}.tar.bz2"
        "https://gitlab.com/kicad/libraries/kicad-symbols/-/archive/${pkgver}/kicad-symbols-${pkgver}.tar.bz2"
        "https://gitlab.com/kicad/libraries/kicad-templates/-/archive/${pkgver}/kicad-templates-${pkgver}.tar.bz2")
sha256sums=('ca3f8b8f5f32f1a9e74dee474674fe0db16656128b23a30c21dae2b7a7e58ca1'
            '8e042e96760e7e07552839ac82bcdca46ce25c0fb8b8216571281d2b70f7e4cb'
            '05ed8e99cd34004b20891a5cec3e33b125af79ef3dd08156a64977b8d0146ab2')

build() {
  for _s in "footprints" "symbols" "templates"; do
    msg2 "Build kicad-${_s}"
    mkdir -p "${srcdir}/build-${_s}-${MSYSTEM}" && cd "${srcdir}/build-${_s}-${MSYSTEM}"

    /usr/bin/cmake.exe \
        -G"Unix Makefiles" \
        -DCMAKE_INSTALL_PREFIX="${MINGW_COMMON_PREFIX}" \
        ../${_realname}-${_s}-${pkgver}

    /usr/bin/cmake.exe --build .
  done
}

package() {
  provides=()
  conflicts=(${MINGW_PACKAGE_PREFIX}-${_realname}-library-git)
  replaces=(${MINGW_PACKAGE_PREFIX}-${_realname}-library-git)

  cd "${srcdir}/build-footprints-${MSYSTEM}"
  DESTDIR="${pkgdir}" /usr/bin/cmake.exe --install .

  cd "${srcdir}/build-symbols-${MSYSTEM}"
  DESTDIR="${pkgdir}" /usr/bin/cmake.exe --install .

  cd "${srcdir}/build-templates-${MSYSTEM}"
  DESTDIR="${pkgdir}" /usr/bin/cmake.exe --install .

  cd "${pkgdir}${MINGW_COMMON_PREFIX}/share" && mv kicad ${_sharefoldername}
}
