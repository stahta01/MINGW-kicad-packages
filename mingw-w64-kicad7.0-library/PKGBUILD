# Support libraries for KiCad

# Based partly on https://archlinux.org/packages/extra/any/kicad-library/ and
# https://github.com/stahta01/MINGW-packages/blob/master/mingw-w64-kicad-library/PKGBUILD

_realname=kicad
_basepkgver=7.0
_basepkgname=${_realname}${_basepkgver}
pkgbase=mingw-w64-${_realname}${_basepkgver}-library
pkgname=("${MINGW_PACKAGE_PREFIX}-${_basepkgname}-library")
pkgver=7.0.9
pkgrel=1
pkgdesc="Symbol, footprint and template libraries for KiCad ${_basepkgver} (mingw-w64)"
arch=('any')
mingw_arch=('mingw64' 'ucrt64' 'clang64' 'mingw32' 'clang32')
url='https://www.kicad.org/libraries/'
license=("Creative Commons CC-BY-SA 4.0 License")
groups=("${MINGW_PACKAGE_PREFIX}-eda")
makedepends=('cmake' 'make')
options=('!strip')
source=("https://gitlab.com/kicad/libraries/kicad-footprints/-/archive/${pkgver}/kicad-footprints-${pkgver}.tar.bz2"
        "https://gitlab.com/kicad/libraries/kicad-symbols/-/archive/${pkgver}/kicad-symbols-${pkgver}.tar.bz2"
        "https://gitlab.com/kicad/libraries/kicad-templates/-/archive/${pkgver}/kicad-templates-${pkgver}.tar.bz2")
sha256sums=('1307f37613c1971794940ee78ad49146ae10eb3525a0c9f427adb8900ddbee0e'
            'c1be547ab435cccf9c98d21c9a816ab3051ae8e306bcbcb4ac46a896c88210ab'
            '3f87dfe352cf12e73cf7769202101935fba547da5f04e1029bdd786ad0f9f0a4')

build() {
  for _s in "footprints" "symbols" "templates"; do
    msg2 "Build kicad-${_s}"
    mkdir -p "${srcdir}/build-${_s}-${MSYSTEM}" && cd "${srcdir}/build-${_s}-${MSYSTEM}"

    /usr/bin/cmake.exe \
        -G"Unix Makefiles" \
        -DCMAKE_INSTALL_PREFIX="${MINGW_PREFIX}" \
        ../${_realname}-${_s}-${pkgver}

    /usr/bin/cmake.exe --build .
  done
}

package() {
  provides=(${MINGW_PACKAGE_PREFIX}-${_realname}-footprints
            ${MINGW_PACKAGE_PREFIX}-${_realname}-symbols
            ${MINGW_PACKAGE_PREFIX}-${_realname}-templates)
  conflicts=(${MINGW_PACKAGE_PREFIX}-${_realname}-footprints
             ${MINGW_PACKAGE_PREFIX}-${_realname}-footprints-git
             ${MINGW_PACKAGE_PREFIX}-${_realname}-symbols
             ${MINGW_PACKAGE_PREFIX}-${_realname}-symbols-git
             ${MINGW_PACKAGE_PREFIX}-${_realname}-templates
             ${MINGW_PACKAGE_PREFIX}-${_realname}-templates-git)
  replaces=(${MINGW_PACKAGE_PREFIX}-${_realname}-footprints-git
            ${MINGW_PACKAGE_PREFIX}-${_realname}-symbols-git
            ${MINGW_PACKAGE_PREFIX}-${_realname}-templates-git)

  cd "${srcdir}/build-footprints-${MSYSTEM}"
  DESTDIR="${pkgdir}" /usr/bin/cmake.exe --install .

  cd "${srcdir}/build-symbols-${MSYSTEM}"
  DESTDIR="${pkgdir}" /usr/bin/cmake.exe --install .

  cd "${srcdir}/build-templates-${MSYSTEM}"
  DESTDIR="${pkgdir}" /usr/bin/cmake.exe --install .
}
