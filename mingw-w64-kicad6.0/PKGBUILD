# Maintainer:  Tim Stahlhut
# Contributor: Alexey Pavlov

MINGW_COMMON_PACKAGE_PREFIX="mingw-common"

_realname=kicad
_basepkgver=6.0
_basepkgname=${_realname}${_basepkgver}
_wx_basever=3.2
options=('!strip' '!debug')
pkgbase=mingw-w64-${_realname}${_basepkgver}
pkgname=("${MINGW_PACKAGE_PREFIX}-${_basepkgname}")
pkgver=${_basepkgver}.11
pkgrel=4
_sourcedir=${_realname}-${pkgver}
pkgdesc="Software for the creation of electronic schematic diagrams and printed circuit board artwork (mingw-w64)"
arch=(any)
mingw_arch=('mingw64' 'ucrt64' 'clang64' 'mingw32' 'clang32')
url="https://www.kicad.org/"
license=("spdx:GPL-3.0-or-later")
groups=("${MINGW_PACKAGE_PREFIX}-eda")
provides=(${MINGW_PACKAGE_PREFIX}-${_realname})
conflicts=(${MINGW_PACKAGE_PREFIX}-${_realname} ${MINGW_PACKAGE_PREFIX}-${_realname}-git)
replaces=(${MINGW_PACKAGE_PREFIX}-${_realname}-git)
depends=(
  "${MINGW_PACKAGE_PREFIX}-boost"
  "${MINGW_PACKAGE_PREFIX}-curl"
  "${MINGW_PACKAGE_PREFIX}-glew"
  "${MINGW_PACKAGE_PREFIX}-glm"
  "${MINGW_PACKAGE_PREFIX}-ngspice"
  "${MINGW_PACKAGE_PREFIX}-python"
  $([[ "$MSYSTEM_CARCH" != "i686" ]] && echo "${MINGW_PACKAGE_PREFIX}-wxPython")
  "${MINGW_PACKAGE_PREFIX}-wxwidgets${_wx_basever}-msw-libs"
  "${MINGW_PACKAGE_PREFIX}-openssl"
  "${MINGW_PACKAGE_PREFIX}-freeglut"
  "${MINGW_PACKAGE_PREFIX}-zlib"
  $([[ ${MINGW_PACKAGE_PREFIX} == *-clang-aarch64* ]] || echo "${MINGW_PACKAGE_PREFIX}-opencascade")
  "${MINGW_PACKAGE_PREFIX}-cairo"
  "${MINGW_COMMON_PACKAGE_PREFIX}-${_basepkgname}-library" # Really optional; but, kicad cannot do much without it.
)
makedepends=(
  'git' 'make' 'patch'
  "${MINGW_PACKAGE_PREFIX}-cc"
  "${MINGW_PACKAGE_PREFIX}-cmake"
  #"${MINGW_PACKAGE_PREFIX}-ninja"
  "${MINGW_PACKAGE_PREFIX}-pkgconf"
  "${MINGW_PACKAGE_PREFIX}-doxygen"
  "${MINGW_PACKAGE_PREFIX}-swig"
  "${MINGW_PACKAGE_PREFIX}-wxwidgets${_wx_basever}-msw"
)
optdepends=()
_doc=("ca" "de" "en" "es" "fr" "id" "it" "ja" "nl" "pl" "ru" "zh")
for _doclang in ${_doc[@]}; do
  optdepends+=("${MINGW_PACKAGE_PREFIX}-${_realname}-doc-${_doclang}: for documentation (${_doclang})");
done
optdepends+=("${MINGW_COMMON_PACKAGE_PREFIX}-${_basepkgname}-library-3d: for 3D model libraries")
source=(
  "https://gitlab.com/kicad/code/kicad/-/archive/${pkgver}/kicad-${pkgver}.tar.bz2"
  '002-ki-6.0-cmake-fixes-for-MINGW-CLANG.patch'
  '003-ki-6.0-code-fixes-for-GNUC-CLANG.patch'
  '006-ki-6.0-rewrite-kiwin32_rc_for_clang.patch'
  '007-ki-6.0-manifest-remove-win10-11-manifest-support.patch'
  '012-ki-6.0-Set-StockEDALibraryPath-to-share-kicad6.0.patch'
  '022-Replace-CURL_LIBRARIES-with-CURL-libcurl.patch'
  '023-CMakeLists.txt-do-not-use-ffloat-store-on-clang.patch'
)
sha256sums=('f2b7ad72942c7e154bebb06d4e6d9fd7ea765f679edafb25b059b387860fbe5d'
            '70eb00dea79f78c02fda26c8b51ac0ae06499f360b43d043c72b6e7bf9cd0972'
            'd8d5f4bdd0aa6d8a907710c523f6f95840636cb2ef69e5275c6ed4966f134353'
            'e03dbb58409145c8fb54991d8259f14bc0ba6b21abca24f9b914ec354c9df89c'
            '550397b8d2ba66b924933020e409e868d3e4b2352a235f329798bc631b9fd28f'
            'e9d62ba01c092f9b535ff75a47bcc130b47069fe0713683cfe6bab4ad24427e5'
            '316fcaec3cbdef5f66d0c79a325fdf6fc823891ceb8e0fa808b79cffefd0ed1f'
            '7b9008c31efc0c0317848933f549655b8ca34cd83c5bd4af448161cb9338384f')

# Helper macros to help make tasks easier #
apply_patch_with_msg() {
  for _fname in "$@"
  do
    msg2 "Applying ${_fname}"
    patch -Nbp1 -i "${srcdir}"/${_fname}
  done
}

prepare() {
  cd ${srcdir}/${_sourcedir}

  apply_patch_with_msg \
    002-ki-6.0-cmake-fixes-for-MINGW-CLANG.patch \
    003-ki-6.0-code-fixes-for-GNUC-CLANG.patch \
    006-ki-6.0-rewrite-kiwin32_rc_for_clang.patch

  # This patch allows me to test view 3D program using my old Intel GPU driver under win10
  apply_patch_with_msg 007-ki-6.0-manifest-remove-win10-11-manifest-support.patch

  # 012 Change StockEDALibraryPath to use MINGW_COMMON_PACKAGE_PREFIX packages
  apply_patch_with_msg \
    012-ki-6.0-Set-StockEDALibraryPath-to-share-kicad6.0.patch

  # Backports from git 7.0 and 8.0 branches
  apply_patch_with_msg \
    022-Replace-CURL_LIBRARIES-with-CURL-libcurl.patch \
    023-CMakeLists.txt-do-not-use-ffloat-store-on-clang.patch
}

build() {
  cd "${srcdir}"
  msg2 "Build kicad"
  [[ -d build-${MSYSTEM} ]] && rm -rf build-${MSYSTEM}
  mkdir -p build-${MSYSTEM} && cd build-${MSYSTEM}

  local -a extra_config
  if check_option "debug" "y"; then
    extra_config+=( -DCMAKE_BUILD_TYPE=Debug )
  else
    extra_config+=( -DCMAKE_BUILD_TYPE=Release )
  fi

  if [[ ${MINGW_PACKAGE_PREFIX} != *-clang-aarch64* ]]; then
    extra_config+=( -DOCC_INCLUDE_DIR=${MINGW_PREFIX}/include/opencascade )
  fi

  if [[ "$MSYSTEM_CARCH" == "i686" ]]; then
    extra_config+=( -DKICAD_SCRIPTING_WXPYTHON=OFF )
  else
    extra_config+=( -DKICAD_SCRIPTING_WXPYTHON=ON )
  fi

  MSYS2_ARG_CONV_EXCL="-DCMAKE_INSTALL_PREFIX=;DCMAKE_PREFIX_PATH=;DEFAULT_INSTALL_PATH=" \
  ${MINGW_PREFIX}/bin/cmake.exe \
    -G "MSYS Makefiles" \
    -DCMAKE_PREFIX_PATH=${MINGW_PREFIX} \
    -DCMAKE_INSTALL_PREFIX=${MINGW_PREFIX} \
    "${extra_config[@]}" \
    -DDEFAULT_INSTALL_PATH=${MINGW_PREFIX} \
    -DwxWidgets_CONFIG_EXECUTABLE=${MINGW_PREFIX}/bin/wx-config-${_wx_basever} \
    -DKICAD_USE_OCC="$( [[ ${MINGW_PACKAGE_PREFIX} == *-clang-aarch64* ]] && echo "OFF" || echo "ON" )" \
    -DKICAD_BUILD_QA_TESTS=OFF \
    -DPYTHON_EXECUTABLE=${MINGW_PREFIX}/bin/python.exe \
    -DPYTHON_INCLUDE_DIR=$(${MINGW_PREFIX}/bin/python.exe -c "from sysconfig import get_paths as gp; print(gp()[\"include\"])") \
    -DPYTHON_ROOT_DIR=${MINGW_PREFIX} \
    -DKICAD_BUILD_I18N=ON \
    ../${_sourcedir}

  cd ${srcdir}/build-${MSYSTEM}
  make # || make VERBOSE=1 --jobs=1
}

package() {
  cd "${srcdir}/build-${MSYSTEM}"
  DESTDIR="${pkgdir}" "${MINGW_PREFIX}"/bin/cmake.exe --install .

  install -Dm644 "${srcdir}/${_realname}-${pkgver}/LICENSE.README" "${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}/LICENSE.README"
  install -Dm644 "${srcdir}/${_realname}-${pkgver}/LICENSE.GPLv3" "${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}/LICENSE.GPLv3"
}
